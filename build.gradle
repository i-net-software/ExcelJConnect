plugins {
  id 'maven-publish'
  id 'io.github.gradle-nexus.publish-plugin' version '2.0.0'
  id 'signing'
  id 'java'
}

group = 'de.inetsoftware'
archivesBaseName = 'exceljconnect'
version = '1.7'

java {
    sourceCompatibility = JavaVersion.VERSION_11
    targetCompatibility = JavaVersion.VERSION_11
}

repositories {
    mavenCentral()
}

dependencies {
    testImplementation 'org.junit.jupiter:junit-jupiter:5.9.3'
    testImplementation 'org.junit.platform:junit-platform-launcher:1.9.3'
}

sourceSets {
    main {
        java {
            srcDirs = ['src']
            include '**/*.java'
        }
        resources {
            srcDirs = ['src']
            exclude '**/*.java'
        }
    }
    test {
        java {
            srcDirs = ['test']
            include '**/*.java'
        }
        resources {
            srcDirs = ['test']
            exclude '**/*.java'
        }
    }
}

java {
    withJavadocJar()
    withSourcesJar()
}

nexusPublishing {
    repositories {
        sonatype {
            nexusUrl = uri('https://ossrh-staging-api.central.sonatype.com/service/local/')
            snapshotRepositoryUrl = uri('https://central.sonatype.com/repository/maven-snapshots/')
            username = project.findProperty('ossrhUsername')
            password = project.findProperty('ossrhPassword')
        }
    }
}
publishing {
    publications {
        maven(MavenPublication) {
            from components.java

            pom {
                name = 'Excel driver for Java'
                description = 'A Excel driver (*.xlsx) written completely in Java (pure Java).'
                url = 'https://github.com/i-net-software/ExcelJConnect'
                licenses {
                    license {
                        name = 'The Apache License, Version 2.0'
                        url = 'http://www.apache.org/licenses/LICENSE-2.0.txt'
                    }
                }
                developers {
                    developer {
                        id = 'HardworkingMarek'
                        name = 'Marek'
                        organization = 'i-net software'
                        organizationUrl = 'https://www.inetsoftware.de'
                    }
                }
                scm {
                    connection = 'scm:git:git@github.com:i-net-software/ExcelJConnect.git'
                    developerConnection = 'scm:git:git@github.com:i-net-software/ExcelJConnect.git'
                    url = 'https://github.com/i-net-software/ExcelJConnect'
                }
            } 
        }
    }
}

signing {
    required { project.hasProperty('signing.keyId') || project.hasProperty('signing.key') }
    if( project.hasProperty('signing.key') ) {
        useInMemoryPgpKeys( project.findProperty('signing.key'), project.findProperty('signing.password') )
    }
    sign publishing.publications.maven
}

closeStagingRepositories.dependsOn 'publishToSonatype'
publish.dependsOn 'closeStagingRepositories'

test {
    useJUnitPlatform()
}
