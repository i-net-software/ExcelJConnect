plugins {
  id 'maven-publish'
  id 'signing'
}

apply plugin: 'java'

repositories {
    mavenCentral()
}

dependencies {
    testImplementation 'org.junit.jupiter:junit-jupiter:5.9.3'
}

sourceSets {
    main {
        java {
            srcDirs = ['src']
            include '**/*.java'
        }
        resources {
            srcDirs = ['src']
            exclude '**/*.java'
        }
    }
    test {
        java {
            srcDirs = ['test']
            include '**/*.java'
        }
        resources {
            srcDirs = ['test']
            exclude '**/*.java'
        }
    }
}

java {
    withJavadocJar()
    withSourcesJar()
}

publishing {
    publications {
        maven(MavenPublication) {
            groupId = 'de.inetsoftware'
            artifactId = 'exceljconnect'
            version = '1.0'
            from components.java
            pom {
                name = 'Excel driver for Java'
                description = 'A Excel driver (*.xlsx) written completely in Java (pure Java).'
                url = 'https://github.com/i-net-software/ExcelJConnect'
                licenses {
                    license {
                        name = 'The Apache License, Version 2.0'
                        url = 'http://www.apache.org/licenses/LICENSE-2.0.txt'
                    }
                }
                scm {
                    connection = 'scm:git:git@github.com:i-net-software/ExcelJConnect.git'
                    developerConnection = 'scm:git:git@github.com:i-net-software/ExcelJConnect.git'
                    url = 'https://github.com/i-net-software/ExcelJConnect'
                }
            } 
        }
    }

    repositories {
        maven {
            name = 'sonatype'
            url = uri("https://oss.sonatype.org/service/local/staging/deploy/maven2/")
            credentials {
                username = project.findProperty("ossrhUsername")
                password = project.findProperty("ossrhPassword")
            }
        }
    }
}

signing {
    sign publishing.publications.maven
}
